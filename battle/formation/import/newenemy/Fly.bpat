% Fly Parts
#define .FlyPart:Main 1

% Fly Actor Variables
#define .FlyActorVar:TurnCount 0`
#define .FlyActorValue:TurnZero 0`
#define .FlyActorValue:TurnOne 1`

% Fly Stats
#define .HP:Fly 2`b
#define .FlyDamage:Charge 1`

#new:Actor $Fly {
% stats
[Index]			D4b
[Level]			0`b
[MaxHP]			.HP:Fly
[Coins]			0`b
[Flags]			00000200
[StatusTable]	$StatusTable_Fly
% ai
[PartsCount]	1`s
[PartsTable]	$PartsTable_Fly
[Script]		$Script_Init_Fly
% move effectiveness
[Escape]		90`b
[Item]			100`b
[AirLift]		100`b
[Hurricane]		100`b % Bow's "Spook" as well
[UpAndAway]		95`b
[PowerBounce]	100`b
[SpinSmash]		0`b % weight (0-4)
% ui positions
[Size]			36`b 26`b % width height
[HealthBar]		0`b 0`b % dx dy
[StatusTurn]	-10`b 20`b % dx dy
[StatusIcon]	10`b 20`b % dx dy
}

#new:DefenseTable $DefenseTable_Fly {
.Element:Normal 0`
.Element:End
}


#new:StatusTable $StatusTable_Fly {
.Status:Normal 				0`
.Status:Default 			0`
.Status:Sleep				100`
.Status:Poison				100`
.Status:Frozen				100`
.Status:Dizzy	 			100`
.Status:Fear				100`
.Status:Static				100`
.Status:Paralyze			100`
.Status:Shrink				100`
.Status:Stop				100`
.Status:DefaultTurnMod		0`
.Status:SleepTurnMod		0`
.Status:PoisonTurnMod		0`
.Status:FrozenTurnMod		0`
.Status:DizzyTurnMod		0`
.Status:FearTurnMod			0`
.Status:StaticTurnMod		0`
.Status:ParalyzeTurnMod		0`
.Status:ShrinkTurnMod		0`
.Status:StopTurnMod			0`
.Status:End
}

#new:PartsTable $PartsTable_Fly % ActorPartBlueprint struct in decomp
{
	/% s32 flags %/						00800000
	/% s8 index %/						~Byte:.FlyPart:Main
	/% Vec3b posOffset %/				0`b 0`b 0`b
	/% Vec2b targetOffset %/			-2`b 15`b % For cursor when selecting to attack in battle
	/% s16 opacity %/					00FFs
	/% s32* IdleAnimations %/			$IdleAnimations_Fly
	/% s32* defenseTable %/				$DefenseTable_Fly
	/% s32 eventFlags %/				00000000
	/% s32 elementImmunityFlags %/		00000000
	/% Vec2b projectileTargetOffset %/	0`b 0`b
	/% s8 unk_1E %/						00b
	/% s8 unk_1F %/						00b
	/% s32 overrideNameMsg %/			00000000
}

#new:IdleAnimations $IdleAnimations_Fly {
.Status:Normal	.Anim:Fly_Idle
.Status:Stone	.Anim:Fly_Idle
.Status:Sleep	.Anim:Fly_Idle
.Status:Poison	.Anim:Fly_Idle
.Status:Stop	.Anim:Fly_Idle
.Status:Static	.Anim:Fly_Idle
.Status:Paralyze	.Anim:Fly_Idle
.Status:Dizzy	.Anim:Fly_Idle
.Status:Fear	.Anim:Fly_Idle
.Status:End
}

#new:Script $Script_Init_Fly {
	Call BindIdle ( .Actor:Self $Script_Idle_Fly )
	Call BindHandleEvent ( .Actor:Self $Script_HandleEvent_Fly )
	Call BindTakeTurn ( .Actor:Self $Script_TakeTurn_Fly )
	Call SetActorVar ( .Actor:Self .FlyActorVar:TurnCount .FlyActorValue:TurnOne )
	Return
	End
}

#new:Script $Script_Idle_Fly {
	Return
	End
}

#new:Script $Script_ReturnHome_Fly {
	Call ResetAllActorSounds ( .Actor:Self )
	Call SetGoalToHome ( .Actor:Self )
	Call SetAnimation ( .Actor:Self .FlyPart:Main .Anim:Fly_Idle )
	Call FlyToGoal ( .Actor:Self 15` 0` .Easing:CosIn )
	Return
	End
}

#new:Script $Script_HandleEvent_Fly {
	Call UseIdleAnimation ( .Actor:Self .False )
	Call EnableIdleScript ( .Actor:Self .False )
	Call GetLastEvent ( .Actor:Self *Var0 )
	Switch *Var0
		CaseOR == .Event:HitCombo % 9
		CaseOR == .Event:Hit % A
			SetConst *Var0 .FlyPart:Main
			SetConst *Var1 .Anim:Fly_Idle % Hurt
			ExecWait DoNormalHit
		EndCaseGroup
		Case == .Event:BurnHit % E
			SetConst *Var0 .FlyPart:Main
			SetConst *Var1 .Anim:Fly_Idle % BurnHurt
			SetConst *Var2 .Anim:Fly_Idle % BurnStill
			ExecWait DoBurnHit
		Case == .Event:BurnDeath % 24
			SetConst *Var0 .FlyPart:Main
			SetConst *Var1 .Anim:Fly_Idle % BurnHurt
			SetConst *Var2 .Anim:Fly_Idle % BurnStill
			ExecWait DoBurnHit
			SetConst *Var0 .FlyPart:Main
			SetConst *Var1 .Anim:Fly_Idle % BurnStill
			ExecWait DoDeath
			Return
		Case == .Event:ShockHit % 2F
			SetConst *Var0 .FlyPart:Main
			SetConst *Var1 .Anim:Fly_Idle % Hurt?
			ExecWait DoShockHit
			SetConst *Var0 .FlyPart:Main
			SetConst *Var1 .Anim:Fly_Idle % Hurt
			ExecWait DoJumpBack
			Call SetAnimation ( .Actor:Self .FlyPart:Main .Anim:Fly_Idle ) % Jump???
			ExecWait $Script_ReturnHome_Fly
		Case == .Event:ShockDeath % 26
			SetConst *Var0 .FlyPart:Main
			SetConst *Var1 .Anim:Fly_Idle % Hurt?
			ExecWait DoShockHit
			SetConst *Var0 .FlyPart:Main
			SetConst *Var1 .Anim:Fly_Idle % Death
			ExecWait DoDeath
			Return
		Case == .Event:StarBeam % 13
		CaseOR == 00000017 % 17
		CaseOR == .Event:Immune % 19
		CaseOR == .Event:AirLiftFailed % 1F
			SetConst *Var0 .FlyPart:Main
			SetConst *Var1 .Anim:Fly_Idle
			ExecWait DoImmune
		EndCaseGroup
		Case == .Event:Death % 20
			SetConst *Var0 .FlyPart:Main
			SetConst *Var1 .Anim:Fly_Idle % Hurt
			ExecWait DoNormalHit
			Wait 10`
			SetConst *Var0 .FlyPart:Main
			SetConst *Var1 .Anim:Fly_Idle % Death
			ExecWait DoDeath
			Return
		Case == .Event:RecoverStatus % 31
			SetConst *Var0 .FlyPart:Main
			SetConst *Var1 .Anim:Fly_Idle
			ExecWait DoRecover
		Default
	EndSwitch
	Call EnableIdleScript ( .Actor:Self .True )
	Call UseIdleAnimation ( .Actor:Self .True )
	Return
	End
}

#new:Script $Script_TakeTurn_Fly {
	Call UseIdleAnimation ( .Actor:Self .False )
	Call EnableIdleScript ( .Actor:Self .False )
	Call GetActorVar ( .Actor:Self .FlyActorVar:TurnCount *Var0 )
	Switch(*Var0)
	Case == .FlyActorValue:TurnZero
		Call SetActorVar ( .Actor:Self .FlyActorVar:TurnCount .FlyActorValue:TurnOne )
	Case == .FlyActorValue:TurnOne
		ExecWait $Script_Attack_Charge_Fly
	EndSwitch

	Call EnableIdleScript ( .Actor:Self .True )
	Call UseIdleAnimation ( .Actor:Self .True )
	Return
	End
}

#new:Script $Script_Attack_Charge_Fly {
	Call UseIdleAnimation ( .Actor:Self .False )
	Call EnableIdleScript ( .Actor:Self .False )
	Call SetTargetActor ( .Actor:Self .Actor:Player )
	Call SetGoalToTarget ( .Actor:Self )
	Call UseBattleCamPreset ( 0000003F )
	Call BattleCamTargetActor ( .Actor:Self )
	Call 8024ECF8 ( FFFFFFFF 00000001 00000000 ) % SetBattleCamTargetingModes
	Call SetAnimation ( .Actor:Self .FlyPart:Main .Anim:Fly_Idle )
	Call EnemyTestTarget ( .Actor:Self *Var0 ~Flags:DamageType:0 00000000 1` 00000010 )
	Switch *Var0
		CaseOR == .HitResult:Miss % 6
		CaseOR == .HitResult:Lucky % 5
			Set *VarA *Var0
			Call GetActorPos ( .Actor:Player *Var0 *Var1 *Var2 )
			Sub *Var0 90`
			Add *Var1 10`
			Add *Var2 2`
			Call SetGoalPos ( .Actor:Self *Var0 *Var1 *Var2 )
			Call FlyToGoal ( .Actor:Self 30` 0` .Easing:CosIn )
			If *VarA == .HitResult:Lucky % 5
				Call EnemyTestTarget ( .Actor:Self *Var0 ~Flags:DamageType:TriggerLucky 00000000 00000000 00000000 )
			EndIf
			Call UseBattleCamPreset ( 00000002 )
			Call YieldTurn ( )
			Call 8027D32C ( .Actor:Self ) % HideHealthBar
			Call UseIdleAnimation ( .Actor:Self .False )
			Call RemoveActor ( .Actor:Self )
			Return
		EndCaseGroup
	EndSwitch
	Call GetActorPos ( .Actor:Player *Var0 *Var1 *Var2 )
	Add *Var0 5`
	Add *Var1 10`
	Add *Var2 2`
	Call SetGoalPos ( .Actor:Self *Var0 *Var1 *Var2 )
	Call FlyToGoal ( .Actor:Self 15` 0` .Easing:CosIn )
	Call SetGoalToTarget ( .Actor:Self )
	Wait 2`
	Call EnemyDamageTarget ( .Actor:Self *Var0 ~Flags:DamageType:0 00000000 00000000 .FlyDamage:Charge 00000020 )
	Call UseBattleCamPreset ( 00000002 )
	Call YieldTurn ( )
	SetConst *Var0 .FlyPart:Main
	SetConst *Var1 .Anim:Fly_Idle
	Set *Var2 FFFFCFC7
	ExecWait DoDeath
	Return
	End
}