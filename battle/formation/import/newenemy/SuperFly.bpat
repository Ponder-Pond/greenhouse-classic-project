% Super Fly Parts
#define .SuperFlyPart:Main 1
#define .SuperFlyPart:Shot 2

% Super Fly Actor Variables
#define .SuperFlyActorVar:TurnCount 0`
% #define .SuperFlyActorValue:TurnZero 0`
#define .SuperFlyActorValue:TurnOne 1`
#define .SuperFlyActorValue:TurnTwo 2`

% Super Fly Stats
#define .HP:SuperFly 4`b
#define .SuperFlyDamage:QuadShot 2`

#new:Actor $SuperFly {
% stats
[Index]			D5b
[Level]			0`b
[MaxHP]			.HP:SuperFly
[Coins]			0`b
[Flags]			00000200
[StatusTable]	$StatusTable_SuperFly
% ai
[PartsCount]	2`s
[PartsTable]	$PartsTable_SuperFly
[Script]		$Script_Init_SuperFly
% move effectiveness
[Escape]		90`b
[Item]			100`b
[AirLift]		100`b
[Hurricane]		100`b % Bow's "Spook" as well
[UpAndAway]		95`b
[PowerBounce]	100`b
[SpinSmash]		0`b % weight (0-4)
% ui positions
[Size]			36`b 26`b % width height
[HealthBar]		0`b 0`b % dx dy
[StatusTurn]	-10`b 20`b % dx dy
[StatusIcon]	10`b 20`b % dx dy
}

#new:DefenseTable $DefenseTable_SuperFly {
.Element:Normal 0`
.Element:End
}


#new:StatusTable $StatusTable_SuperFly {
.Status:Normal 				0`
.Status:Default 			0`
.Status:Sleep				100`
.Status:Poison				100`
.Status:Frozen				100`
.Status:Dizzy	 			100`
.Status:Fear				100`
.Status:Static				100`
.Status:Paralyze			100`
.Status:Shrink				100`
.Status:Stop				100`
.Status:DefaultTurnMod		0`
.Status:SleepTurnMod		0`
.Status:PoisonTurnMod		0`
.Status:FrozenTurnMod		0`
.Status:DizzyTurnMod		0`
.Status:FearTurnMod			0`
.Status:StaticTurnMod		0`
.Status:ParalyzeTurnMod		0`
.Status:ShrinkTurnMod		0`
.Status:StopTurnMod			0`
.Status:End
}

#new:PartsTable $PartsTable_SuperFly % ActorPartBlueprint struct in decomp
{
	/% s32 flags %/						00800000
	/% s8 index %/						~Byte:.SuperFlyPart:Main
	/% Vec3b posOffset %/				0`b 0`b 0`b
	/% Vec2b targetOffset %/			-2`b 15`b % For cursor when selecting to attack in battle
	/% s16 opacity %/					00FFs
	/% s32* IdleAnimations %/			$IdleAnimations_SuperFly
	/% s32* defenseTable %/				$DefenseTable_SuperFly
	/% s32 eventFlags %/				00000000
	/% s32 elementImmunityFlags %/		00000000
	/% Vec2b projectileTargetOffset %/	0`b 0`b
	/% s8 unk_1E %/						00b
	/% s8 unk_1F %/						00b
	/% s32 overrideNameMsg %/			00000000

    /% Shot %/
	/% s32 flags %/						00120001
	/% s8 index %/						~Byte:.SuperFlyPart:Shot
	/% Vec3b posOffset %/				0`b 0`b 0`b
	/% Vec2b targetOffset %/			0`b 0`b % For cursor when selecting to attack in battle
	/% s16 opacity %/					00FFs
	/% s32* IdleAnimations %/			$IdleAnimations_SuperFly %? No Shot Animation yet.
	/% s32* defenseTable %/				$DefenseTable_SuperFly
	/% s32 eventFlags %/				00000000
	/% s32 elementImmunityFlags %/		00000000
	/% Vec2b projectileTargetOffset %/	0`b 0`b
	/% s8 unk_1E %/						00b
	/% s8 unk_1F %/						00b
	/% s32 overrideNameMsg %/			00000000
}

#new:IdleAnimations $IdleAnimations_SuperFly {
.Status:Normal	    .Anim:SuperFly_Idle
.Status:Stone	    .Anim:SuperFly_Idle
.Status:Sleep	    .Anim:SuperFly_Idle
.Status:Poison	    .Anim:SuperFly_Idle
.Status:Stop	    .Anim:SuperFly_Idle
.Status:Static	    .Anim:SuperFly_Idle
.Status:Paralyze	.Anim:SuperFly_Idle
.Status:Dizzy	    .Anim:SuperFly_Idle
.Status:Fear	    .Anim:SuperFly_Idle
.Status:End
}

#new:IdleAnimations $IdleAnimations_Attack_SuperFly {
.Status:Normal		.Anim:SuperFly_Attack
.Status:End
}

#new:Script $Script_Init_SuperFly {
	Call BindIdle ( .Actor:Self $Script_Idle_SuperFly )
	Call BindHandleEvent ( .Actor:Self $Script_HandleEvent_SuperFly )
	Call BindTakeTurn ( .Actor:Self $Script_TakeTurn_SuperFly )
	Call SetActorVar ( .Actor:Self .SuperFlyActorVar:TurnCount .SuperFlyActorValue:TurnOne )
	Return
	End
}

#new:Script $Script_Idle_SuperFly {
	Return
	End
}

#new:Script $Script_ReturnHome_SuperFly {
	Call ResetAllActorSounds ( .Actor:Self )
	Call SetGoalToHome ( .Actor:Self )
	Call FlyToGoal ( .Actor:Self 15` 0` .Easing:CosIn )
	Call SetAnimation ( .Actor:Self .SuperFlyPart:Main .Anim:SuperFly_Idle )
	Return
	End
}

#new:Script $Script_HandleEvent_SuperFly {
	Call UseIdleAnimation ( .Actor:Self .False )
	Call EnableIdleScript ( .Actor:Self .False )
	Call GetLastEvent ( .Actor:Self *Var0 )
	Switch *Var0
		CaseOR == .Event:HitCombo % 9
		CaseOR == .Event:Hit % A
			SetConst *Var0 .SuperFlyPart:Main
			SetConst *Var1 .Anim:SuperFly_Idle % Hurt
			ExecWait DoNormalHit
		EndCaseGroup
		Case == .Event:BurnHit % E
			SetConst *Var0 .SuperFlyPart:Main
			SetConst *Var1 .Anim:SuperFly_Idle % BurnHurt
			SetConst *Var2 .Anim:SuperFly_Idle % BurnStill
			ExecWait DoBurnHit
		Case == .Event:BurnDeath % 24
			SetConst *Var0 .SuperFlyPart:Main
			SetConst *Var1 .Anim:SuperFly_Idle % BurnHurt
			SetConst *Var2 .Anim:SuperFly_Idle % BurnStill
			ExecWait DoBurnHit
			SetConst *Var0 .SuperFlyPart:Main
			SetConst *Var1 .Anim:SuperFly_Idle % BurnStill
			ExecWait DoDeath
			Return
		Case == .Event:ShockHit % 2F
			SetConst *Var0 .SuperFlyPart:Main
			SetConst *Var1 .Anim:SuperFly_Idle % Hurt?
			ExecWait DoShockHit
			SetConst *Var0 .SuperFlyPart:Main
			SetConst *Var1 .Anim:SuperFly_Idle % Hurt
			ExecWait DoJumpBack
			Call SetAnimation ( .Actor:Self .SuperFlyPart:Main .Anim:SuperFly_Idle ) % Jump???
			ExecWait $Script_ReturnHome_SuperFly
		Case == .Event:ShockDeath % 26
			SetConst *Var0 .SuperFlyPart:Main
			SetConst *Var1 .Anim:SuperFly_Idle % Hurt?
			ExecWait DoShockHit
			SetConst *Var0 .SuperFlyPart:Main
			SetConst *Var1 .Anim:SuperFly_Idle % Death
			ExecWait DoDeath
			Return
		Case == .Event:StarBeam % 13
		CaseOR == 00000017 % 17
		CaseOR == .Event:Immune % 19
		CaseOR == .Event:AirLiftFailed % 1F
			SetConst *Var0 .SuperFlyPart:Main
			SetConst *Var1 .Anim:SuperFly_Idle
			ExecWait DoImmune
		EndCaseGroup
		Case == .Event:Death % 20
			SetConst *Var0 .SuperFlyPart:Main
			SetConst *Var1 .Anim:SuperFly_Idle % Hurt
			ExecWait DoNormalHit
			Wait 10`
			SetConst *Var0 .SuperFlyPart:Main
			SetConst *Var1 .Anim:SuperFly_Idle % Death
			ExecWait DoDeath
			Return
		Case == .Event:RecoverStatus % 31
			SetConst *Var0 .SuperFlyPart:Main
			SetConst *Var1 .Anim:SuperFly_Idle
			ExecWait DoRecover
		Default
	EndSwitch
	Call EnableIdleScript ( .Actor:Self .True )
	Call UseIdleAnimation ( .Actor:Self .True )
	Return
	End
}

#new:Script $Script_TakeTurn_SuperFly {
	Call UseIdleAnimation ( .Actor:Self .False )
	Call EnableIdleScript ( .Actor:Self .False )
	Call GetActorVar ( .Actor:Self .SuperFlyActorVar:TurnCount *Var0 )
	Switch(*Var0)
	Case == .SuperFlyActorValue:TurnOne
        Call GetStatusFlags ( .Actor:Self *Var0 )
        If *Var0 & ~Flags:StatusFlags:Sleep|Frozen|Fear|Paralyze|Dizzy|Stone|Stop % 35D000
            Return
        EndIf
		Call UseBattleCamPreset ( 0000000D ) % BTL_CAM_ACTOR_CLOSE
		Call BattleCamTargetActor ( .Actor:Self )
		Call 8024ECF8 ( FFFFFFFF 00000001 00000000 ) % SetBattleCamTargetingModes
		Call SetBattleCamZoom ( 250` )
		Call SetBattleCamOffsetZ ( 35` )
		Call MoveBattleCamOver ( 20` )
		Wait 20`
		Call PlaySoundAtActor ( .Actor:Self .Sound:PowerUp )
		Call SetAnimation ( .Actor:Self .SuperFlyPart:Main .Anim:SuperFly_Attack )
        Call SetIdleAnimations ( .Actor:Self .SuperFlyPart:Main $IdleAnimations_Attack_SuperFly )
		Wait 30`
		Call UseBattleCamPreset ( 00000002 ) % BTL_CAM_DEFAULT
		Call MoveBattleCamOver ( 20` )
		Wait 20`
		Call SetActorVar ( .Actor:Self .SuperFlyActorVar:TurnCount .SuperFlyActorValue:TurnTwo )
	Case == .SuperFlyActorValue:TurnTwo
        Call GetStatusFlags ( .Actor:Self *Var0 )
        If *Var0 & ~Flags:StatusFlags:Sleep|Frozen|Fear|Paralyze|Dizzy|Stone|Stop % 35D000
            Return
        EndIf
		ExecWait $Script_Attack_QuadShot_SuperFly
	EndSwitch

	Call EnableIdleScript ( .Actor:Self .True )
	Call UseIdleAnimation ( .Actor:Self .True )
	Return
	End
}

%%%%% & WIP Scripts & %%%%%

#new:Script $Script_Attack_QuadShot_SuperFly {
	Call UseIdleAnimation ( .Actor:Self .False )
	Call EnableIdleScript ( .Actor:Self .False )
	Call SetTargetActor ( .Actor:Self .Actor:Player )
	Call SetGoalToTarget ( .Actor:Self )
	Call UseBattleCamPreset ( 0000003F )
	Call BattleCamTargetActor ( .Actor:Self )
	Call 8024ECF8 ( FFFFFFFF 00000001 00000000 ) % SetBattleCamTargetingModes
	Call SetAnimation ( .Actor:Self .SuperFlyPart:Main .Anim:SuperFly_Attack )
	Call EnemyTestTarget ( .Actor:Self *Var0 ~Flags:DamageType:0 00000000 1` 00000010 )
	Switch *Var0
		CaseOR == .HitResult:Miss % 6
		CaseOR == .HitResult:Lucky % 5
			Set *VarA *Var0
			Call GetActorPos ( .Actor:Player *Var0 *Var1 *Var2 )
			Sub *Var0 90`
			Add *Var1 10`
			Add *Var2 2`
			Call SetGoalPos ( .Actor:Self *Var0 *Var1 *Var2 )
			Call FlyToGoal ( .Actor:Self 30` 0` .Easing:CosIn )
			If *VarA == .HitResult:Lucky % 5
				Call EnemyTestTarget ( .Actor:Self *Var0 ~Flags:DamageType:TriggerLucky 00000000 00000000 00000000 )
			EndIf
			Call UseBattleCamPreset ( 00000002 )
			Call YieldTurn ( )
			Call 8027D32C ( .Actor:Self ) % HideHealthBar
			Call UseIdleAnimation ( .Actor:Self .False )
			Call RemoveActor ( .Actor:Self )
			Return
		EndCaseGroup
	EndSwitch
	Call GetActorPos ( .Actor:Player *Var0 *Var1 *Var2 )
	Add *Var0 5`
	Add *Var1 10`
	Add *Var2 2`
	Call SetGoalPos ( .Actor:Self *Var0 *Var1 *Var2 )
	Call FlyToGoal ( .Actor:Self 15` 0` .Easing:CosIn )
	Call SetGoalToTarget ( .Actor:Self )
	Wait 2`
	Call EnemyDamageTarget ( .Actor:Self *Var0 ~Flags:DamageType:0 00000000 00000000 .SuperFlyDamage:QuadShot 00000020 )
	Call UseBattleCamPreset ( 00000002 )
	Call YieldTurn ( )
	SetConst *Var0 .SuperFlyPart:Main
	SetConst *Var1 .Anim:SuperFly_Attack
	Set *Var2 FFFFCFC7
	ExecWait DoDeath
	Return
	End
}