% Bomb Fly Parts
#define .BombFlyPart:Main 1

% Bomb Fly Actor Variables
#define .BombFlyActorVar:TurnCount 0`
% #define .BombFlyActorValue:TurnZero 0`
#define .BombFlyActorValue:TurnOne 1`
#define .BombFlyActorValue:TurnTwo 2`
#define .BombFlyActorVar:HitDuringCombo 1`
#define .BombFlyActorVar:Ignited 2`


% Bomb Fly Stats
#define .HP:BombFly 6`b
#define .BombFlyDamage:ChargeBlast 6`

#new:Actor $BombFly {
% stats
[Index]			D7b
[Level]			6`b
[MaxHP]			.HP:BombFly
[Coins]			0`b
[Flags]			00000200
[StatusTable] 	$StatusTable_BombFly
% ai
[PartsCount]	1`s
[PartsTable]	$PartsTable_BombFly
[Script]		$Script_Init_BombFly
% move effectiveness
[Escape]		90`b
[Item]			100`b
[AirLift]		100`b
[Hurricane]		100`b % Bow's "Spook" as well
[UpAndAway]		95`b
[PowerBounce]	100`b
[SpinSmash]		0`b % weight (0-4)
% ui positions
[Size]			36`b 26`b % width height
[HealthBar]		0`b 0`b % dx dy
[StatusTurn]	-10`b 20`b % dx dy
[StatusIcon]	10`b 20`b % dx dy
}

#new:DefenseTable $DefenseTable_BombFly {
.Element:Normal 0`
.Element:End
}


#new:StatusTable $StatusTable_BombFly {
.Status:Normal				0`
.Status:Default				0`
.Status:Sleep				100`
.Status:Poison				100`
.Status:Frozen				100`
.Status:Dizzy				100`
.Status:Fear				100`
.Status:Static				100`
.Status:Paralyze			100`
.Status:Shrink				100`
.Status:Stop				100`
.Status:DefaultTurnMod		0`
.Status:SleepTurnMod		0`
.Status:PoisonTurnMod		0`
.Status:FrozenTurnMod		0`
.Status:DizzyTurnMod		0`
.Status:FearTurnMod			0`
.Status:StaticTurnMod		0`
.Status:ParalyzeTurnMod		0`
.Status:ShrinkTurnMod		0`
.Status:StopTurnMod			0`
.Status:End
}

#new:PartsTable $PartsTable_BombFly % ActorPartBlueprint struct in decomp
{
	/% s32 flags %/						00800000
	/% s8 index %/						~Byte:.BombFlyPart:Main
	/% Vec3b posOffset %/				0`b 0`b 0`b
	/% Vec2b targetOffset %/			-3`b 20`b % For cursor when selecting to attack in battle
	/% s16 opacity %/					00FFs
	/% s32* IdleAnimations %/			$IdleAnimations_BombFly
	/% s32* defenseTable %/				$DefenseTable_BombFly
	/% s32 eventFlags %/				00000000
	/% s32 elementImmunityFlags %/		00000000
	/% Vec2b projectileTargetOffset %/	0`b 0`b
	/% s8 unk_1E %/						00b
	/% s8 unk_1F %/						00b
	/% s32 overrideNameMsg %/			00000000
}

#new:IdleAnimations $IdleAnimations_BombFly {
.Status:Normal		.Anim:BombFly_Idle
.Status:Stone		.Anim:BombFly_Idle
.Status:Sleep		.Anim:BombFly_Idle
.Status:Poison		.Anim:BombFly_Idle
.Status:Stop		.Anim:BombFly_Idle
.Status:Static		.Anim:BombFly_Idle
.Status:Paralyze	.Anim:BombFly_Idle
.Status:Dizzy		.Anim:BombFly_Idle
.Status:Fear		.Anim:BombFly_Idle
.Status:End
}

#new:IdleAnimations $IdleAnimations_Ignited_BombFly {
.Status:Normal		.Anim:BombFly_Attack
.Status:End
}

#new:Script $Script_Init_BombFly {
	Call BindIdle ( .Actor:Self $Script_Idle_BombFly )
	Call BindHandleEvent ( .Actor:Self $Script_HandleEvent_BombFly )
	Call BindTakeTurn ( .Actor:Self $Script_TakeTurn_BombFly )
	Call SetActorVar ( .Actor:Self .BombFlyActorVar:TurnCount .BombFlyActorValue:TurnOne )
	Call SetActorVar ( .Actor:Self .BombFlyActorVar:HitDuringCombo .False )
	Call SetActorVar ( .Actor:Self .BombFlyActorVar:Ignited .False )
	Return
	End
}

#new:Script $Script_Idle_BombFly {
	Return
	End
}

#new:Script $Script_ReturnHome_BombFly {
	Call ResetAllActorSounds ( .Actor:Self )
	Call SetGoalToHome ( .Actor:Self )
	Call FlyToGoal ( .Actor:Self 15` 0` .Easing:Linear )
	Call SetAnimation ( .Actor:Self .BombFlyPart:Main .Anim:BombFly_Idle )
	Return
	End
}

#new:Script $Script_HandleEvent_BombFly {
	Call UseIdleAnimation ( .Actor:Self .False )
	Call EnableIdleScript ( .Actor:Self .False )
	Call GetLastEvent ( .Actor:Self *Var0 )
	Switch *Var0
		Case == .Event:HitCombo % 9
			Call GetLastDamage ( .Actor:Self *Var0 )
			If *Var0 != 0`
				Call SetActorVar ( .Actor:Self .BombFlyActorVar:HitDuringCombo .True )
			EndIf
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % Hurt
			ExecWait DoNormalHit
		Case == .Event:Hit % A
			Call GetLastElement ( *VarE )
			If *VarE & 00000020 % DAMAGE_TYPE_SHOCK
				SetConst *Var0 .BombFlyPart:Main
				SetConst *Var1 .Anim:BombFly_Idle % BurnHurt
				SetConst *Var2 .Anim:BombFly_Idle % BurnStill
				ExecWait DoBurnHit
				ExecWait $Script_Explode_BombFly
				SetConst *Var0 .BombFlyPart:Main
				SetConst *Var1 .Anim:BombFly_Idle % BurnStill
				Set *Var2 FFFFCFC7
				ExecWait DoDeath
				Return
			Else
				SetConst *Var0 .BombFlyPart:Main
				SetConst *Var1 .Anim:BombFly_Idle % Hurt
				ExecWait DoNormalHit
				Call GetLastDamage ( .Actor:Self *Var0 )
				If *Var0 != 0`
					ExecWait $Script_Ignite_Bombfly
				EndIf
			EndIf
		CaseOR == .Event:BurnHit % E
		CaseOR == .Event:BurnDeath % 24
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % BurnHurt
			SetConst *Var2 .Anim:BombFly_Idle % BurnStill
			ExecWait DoBurnHit
			ExecWait $Script_Explode_BombFly
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % BurnStill
			Set *Var2 FFFFCFC7
			ExecWait DoDeath
			Return
		EndCaseGroup
		Case == .Event:SpinSmashHit % B
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % Hurt
			ExecWait DoSpinSmashHit
			Call GetLastDamage ( .Actor:Self *Var0 )
			If *Var0 != 0`
				ExecWait $Script_Ignite_Bombfly
			EndIf
		Case == .Event:SpinSmashDeath % 21
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % Hurt
			ExecWait DoSpinSmashHit
			% ExecWait $Script_Cleanup_BombFly %? Not sure if we are going to use this or not
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % Hurt
			ExecWait DoDeath
			Return
		CaseOR == .Event:ShockHit % 2F
		CaseOR == .Event:ShockDeath % 26
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % BurnHurt
			Set *Var2 FFFFCFC7
			ExecWait DoDeath
			Return
		Case == 00000017 % 17
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle
			ExecWait DoImmune
		CaseOR == .Event:Immune % 19
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle
			ExecWait DoImmune
			Call GetActorVar ( .Actor:Self .BombFlyActorVar:HitDuringCombo *Var0 )
			If *Var0 == .True
				ExecWait $Script_Ignite_Bombfly
			EndIf
		EndCaseGroup
		Case == .Event:Death % 20
			Call GetLastElement ( *VarE )
			If *VarE & 00000020 % DAMAGE_TYPE_SHOCK
				SetConst *Var0 .BombFlyPart:Main
				SetConst *Var1 .Anim:BombFly_Idle % BurnHurt
				SetConst *Var2 .Anim:BombFly_Idle % BurnStill
				ExecWait DoBurnHit
				ExecWait $Script_Explode_BombFly
				SetConst *Var0 .BombFlyPart:Main
				SetConst *Var1 .Anim:BombFly_Idle % BurnStill
				Set *Var2 FFFFCFC7
				ExecWait DoDeath
				Return
			Else
				SetConst *Var0 .BombFlyPart:Main
				SetConst *Var1 .Anim:BombFly_Idle % Hurt
				ExecWait DoNormalHit
				Wait 10`
				% ExecWait $Script_Cleanup_BombFly %? Not sure if we are going to use this or not
				SetConst *Var0 .BombFlyPart:Main
				SetConst *Var1 .Anim:BombFly_Idle % Hurt
				ExecWait DoDeath
			EndIf
			Return
		Case == .Event:ExplodeTrigger % 22
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % BurnHurt
			SetConst *Var2 .Anim:BombFly_Idle % BurnStill
			ExecWait DoBurnHit
			ExecWait $Script_Explode_BombFly
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % BurnStill
			Set *Var2 FFFFCFC7
			ExecWait DoDeath
			Return
		Case == .Event:RecoverStatus % 31
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle
			ExecWait DoRecover
		Default
	EndSwitch
	Call EnableIdleScript ( .Actor:Self .True )
	Call UseIdleAnimation ( .Actor:Self .True )
	Return
	End
}

#new:Script $Script_HandleEvent_Ignited_BombFly {
	Call UseIdleAnimation ( .Actor:Self .False )
	Call EnableIdleScript ( .Actor:Self .False )
	Call GetLastEvent ( .Actor:Self *Var0 )
	Switch *Var0
		Case == .Event:HitCombo % 9
			Call GetLastElement ( *VarE )
			If *VarE & 00000004 % DAMAGE_TYPE_WATER
				ExecWait $Script_Defuse_BombFly
				SetConst *Var0 .BombFlyPart:Main
				SetConst *Var1 .Anim:BombFly_Idle % Hurt
				ExecWait DoNormalHit
			Else
				SetConst *Var0 .BombFlyPart:Main
				SetConst *Var1 .Anim:BombFly_Idle % HurtLit
				ExecWait DoNormalHit
			EndIf
		Case == .Event:Hit % A
			Call GetLastElement ( *VarE )
			Switch *VarE
				Case & 00000004 % DAMAGE_TYPE_WATER
					ExecWait $Script_Defuse_BombFly
					SetConst *Var0 .BombFlyPart:Main
					SetConst *Var1 .Anim:BombFly_Idle % Hurt
					ExecWait DoNormalHit
				Case & 00000020 % DAMAGE_TYPE_SHOCK
					SetConst *Var0 .BombFlyPart:Main
					SetConst *Var1 .Anim:BombFly_Idle % BurnHurt
					ExecWait DoNormalHit
					ExecWait $Script_Explode_BombFly
					SetConst *Var0 .BombFlyPart:Main
					SetConst *Var1 .Anim:BombFly_Idle % BurnStill
					Set *Var2 FFFFCFC7
					ExecWait DoDeath
					Return
				Default
					SetConst *Var0 .BombFlyPart:Main
					SetConst *Var1 .Anim:BombFly_Idle % HurtLit
					ExecWait DoNormalHit
					Call GetLastDamage ( .Actor:Self *Var0 )
					If *Var0 > 0`
						ExecWait $Script_Explode_BombFly
						SetConst *Var0 .BombFlyPart:Main
						SetConst *Var1 .Anim:BombFly_Idle % BurnStill
						Set *Var2 FFFFCFC7
						ExecWait DoDeath
						Return
					EndIf
			EndSwitch
		CaseOR == .Event:BurnHit % E
		CaseOR == .Event:BurnDeath % 24
		CaseOR == .Event:SpinSmashHit % B
		CaseOR == .Event:SpinSmashDeath % 21
		CaseOR == .Event:ExplodeTrigger % 22
			ExecWait $Script_Explode_BombFly
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % BurnStill
			Set *Var2 FFFFCFC7
			ExecWait DoDeath
			Return
		EndCaseGroup
		CaseOR == 00000017 % 17
		CaseOR == .Event:Immune % 19
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % IdleLit
			ExecWait DoImmune
		EndCaseGroup
		Case == .Event:Death % 20
			Call GetLastElement ( *VarE )
			If *VarE & 00000004
				ExecWait $Script_Defuse_BombFly
				SetConst *Var0 .BombFlyPart:Main
				SetConst *Var1 .Anim:BombFly_Idle % Hurt
			Else
				ExecWait $Script_Explode_BombFly
				SetConst *Var0 .BombFlyPart:Main
				SetConst *Var1 .Anim:BombFly_Idle % BurnStill
				Set *Var2 FFFFCFC7
			EndIf
			ExecWait DoDeath
			Return
		CaseOR == .Event:ShockHit % 2F
		CaseOR == .Event:ShockDeath % 26
			ExecWait $Script_Explode_BombFly
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % BurnStill
			Set *Var2 FFFFCFC7
			ExecWait DoDeath
			Return
		EndCaseGroup
		Case == .Event:RecoverStatus % 31
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Idle % IdleLit
			ExecWait DoRecover
		Default
	EndSwitch
	Call EnableIdleScript ( .Actor:Self .True )
	Call UseIdleAnimation ( .Actor:Self .True )
	Return
	End
}

#new:Script $Script_Ignite_Bombfly {
	Call GetStatusFlags ( .Actor:Self *Var0 )
	If *Var0 & ~Flags:StatusFlags:Sleep|Frozen|Fear|Paralyze|Dizzy|Stone|Stop % 35D000
		Return
	EndIf
	Call UseBattleCamPreset ( 0000000D ) % BTL_CAM_ACTOR_CLOSE
	Call BattleCamTargetActor ( .Actor:Self )
	Call 8024ECF8 ( FFFFFFFF 00000001 00000000 ) % SetBattleCamTargetingModes
	Call SetBattleCamZoom ( 250` )
	Call SetBattleCamOffsetZ ( 35` )
	Call MoveBattleCamOver ( 40` )
	Call PlaySoundAtActor ( .Actor:Self .Sound:PowerUp )
	Call SetAnimation ( .Actor:Self .BombFlyPart:Main .Anim:BombFly_Attack )
	Call SetIdleAnimations ( .Actor:Self .BombFlyPart:Main $IdleAnimations_Ignited_BombFly )
	Call BindHandleEvent ( .Actor:Self $Script_HandleEvent_Ignited_BombFly )
	Call SetPartEventBits ( .Actor:Self .BombFlyPart:Main 00008000 .True )
	Wait 30`
	Return
	End
}

#new:Script $Script_Explode_BombFly {
	% ExecWait $Script_Cleanup_BombFly %? Not sure if we are going to use this or not
	Call StartRumble ( 0000000B )
	Thread
		Call ShakeCam ( .Cam:Battle 0` 2` *Fixed[0.75] )
		Call ShakeCam ( .Cam:Battle 0` 5` *Fixed[3.0] )
		Call ShakeCam ( .Cam:Battle 0` 10` *Fixed[4.5] )
		Call ShakeCam ( .Cam:Battle 0` 5` *Fixed[3.0] )
	EndThread
	Call GetActorPos ( .Actor:Self *Var0 *Var1 *Var2 )
	Add *Var2 2`
	Call PlayEffect ( ~FX:SmokeRing:Blast1 *Var0 *Var1 *Var2 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
	Add *Var1 20`
	Add *Var2 2`
	Call PlayEffect ( ~FX:Explosion1 *Var0 *Var1 *Var2 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )
	Call PlaySoundAtActor ( .Actor:Self .Sound:BombBlast )
	Return
	End
}

#new:Script $Script_Defuse_BombFly {
	Call BindHandleEvent ( .Actor:Self $Script_HandleEvent_BombFly )
	Call SetActorVar ( .Actor:Self .BombFlyActorVar:Ignited .False )
	Call SetIdleAnimations ( .Actor:Self .BombFlyPart:Main $IdleAnimations_BombFly )
	Call SetPartEventBits ( .Actor:Self .BombFlyPart:Main 00008000 .False )
	Return
	End
}

#new:Script $Script_TakeTurn_BombFly {
	Call UseIdleAnimation ( .Actor:Self .False )
	Call EnableIdleScript ( .Actor:Self .False )
	Call GetActorVar ( .Actor:Self .BombFlyActorVar:TurnCount *Var0 )
	Switch(*Var0)
	Case == .BombFlyActorValue:TurnOne
		ExecWait $Script_Ignite_Bombfly
		Call SetActorVar ( .Actor:Self .BombFlyActorVar:TurnCount .BombFlyActorValue:TurnTwo )
	Case == .BombFlyActorValue:TurnTwo
		ExecWait $Script_Attack_BlastCharge_BombFly
	EndSwitch

	Call EnableIdleScript ( .Actor:Self .True )
	Call UseIdleAnimation ( .Actor:Self .True )
	Return
	End
}

#new:Script $Script_Attack_BlastCharge_BombFly {
	Call UseIdleAnimation ( .Actor:Self .False )
	Call EnableIdleScript ( .Actor:Self .False )
	Call SetTargetActor ( .Actor:Self .Actor:Player )
	Call SetTargetActor ( .Actor:Self .Actor:Player )
	Call SetGoalToTarget ( .Actor:Self )
	Call UseBattleCamPreset ( 0000003F )
	Call BattleCamTargetActor ( .Actor:Self )
	Call 8024ECF8 ( FFFFFFFF 00000001 00000000 ) % SetBattleCamTargetingModes
	Call SetAnimation ( .Actor:Self .BombFlyPart:Main .Anim:BombFly_Attack )
	Call 8024E664 ( 00000013 ) % UseBattleCamPresetImmediately
	Call EnemyTestTarget ( .Actor:Self *Var0 ~Flags:DamageType:0 00000000 1` 00000010 )
	Switch *Var0
		CaseOR == .HitResult:Miss % 6
		CaseOR == .HitResult:Lucky % 5
			Set *VarA *Var0
			Call GetActorPos ( .Actor:Player *Var0 *Var1 *Var2 )
			Sub *Var0 90`
			Add *Var1 10`
			Add *Var2 2`
			Call SetGoalPos ( .Actor:Self *Var0 *Var1 *Var2 )
			Call FlyToGoal ( .Actor:Self 30` 0` .Easing:Linear )
			Call UseBattleCamPreset ( 00000002 )
			ExecWait $Script_Explode_BombFly
			Wait 10`
			If *VarA == .HitResult:Lucky % 5
				Call EnemyTestTarget ( .Actor:Self *Var0 ~Flags:DamageType:TriggerLucky 00000000 00000000 00000000 )
			EndIf
			Call YieldTurn ( )
			SetConst *Var0 .BombFlyPart:Main
			SetConst *Var1 .Anim:BombFly_Attack
			Set *Var2 FFFFCFC7
			ExecWait DoDeath
			Return
		EndCaseGroup
	EndSwitch
	Call GetActorPos ( .Actor:Player *Var0 *Var1 *Var2 )
	Add *Var0 5`
	Add *Var1 10`
	Add *Var2 2`
	Call SetGoalPos ( .Actor:Self *Var0 *Var1 *Var2 )
	Call FlyToGoal ( .Actor:Self 15` 0` .Easing:Linear )
	Call SetGoalToTarget ( .Actor:Self )
	Set *VarA 1`
	ExecWait $Script_Explode_BombFly
	Wait 2`
	Call EnemyDamageTarget ( .Actor:Self *Var0 ~Flags:DamageType:NoContact|Blast 00000000 00000000 .BombFlyDamage:ChargeBlast 00000020 )
	Call UseBattleCamPreset ( 00000002 )
	Call YieldTurn ( )
	SetConst *Var0 .BombFlyPart:Main
	SetConst *Var1 .Anim:BombFly_Attack
	Set *Var2 FFFFCFC7
	ExecWait DoDeath
	Return
	End
}

%%%%% & Unused Scripts & %%%%%
/%
#new:Script $Script_Cleanup_BombFly {
	Call GetActorVar ( .Actor:Self .BombFlyActorVar:Ignited *Var0 )
	If *Var0 == .True
		% Call StopLoopingSoundAtActor ( .Actor:Self 0` )
		% Call 8026ED20 ( .Actor:Self .BombFlyPart:Main .False ) % EnableActorPaletteEffects
	EndIf
	Return
	End
}
%/